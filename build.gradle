/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.elasticsearch.gradle.MavenFilteringHack
import org.elasticsearch.gradle.VersionProperties
import org.elasticsearch.gradle.test.RestIntegTestTask

version = '0.1.38'

buildscript {
  repositories {
    jcenter()
    maven {
      url "https://artifacts.elastic.co/maven"
    }
  }

  dependencies {
    classpath "org.elasticsearch.gradle:build-tools:5.6.2"
  }
}

apply plugin: 'elasticsearch.build'
apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
  jcenter()
  maven {
    url "https://artifacts.elastic.co/maven"
  }
}

ext.projectSubstitutions = [:]
project.licenseFile = project.rootProject.file("LICENSE.txt")
project.noticeFile = project.rootProject.file("NOTICE.txt")

dependencies {
  def esVersion = '5.6.2'
  def xpackVersion = '5.6.1'
  def googleOAuthVersion = '1.22.0'
  def log4jver = '2.9.1'

  provided "org.elasticsearch:elasticsearch:${esVersion}"
  provided "org.elasticsearch.plugin:x-pack-api:${xpackVersion}"
  provided "org.elasticsearch:jna:4.4.0-1"
  provided "org.apache.logging.log4j:log4j-api:${log4jver}"
  provided "org.apache.logging.log4j:log4j-core:${log4jver}"

  compile "com.google.oauth-client:google-oauth-client:${googleOAuthVersion}"
  compile "com.google.oauth-client:google-oauth-client-java6:${googleOAuthVersion}"
  compile "com.google.http-client:google-http-client:${googleOAuthVersion}"
  compile "com.google.http-client:google-http-client-jackson2:${googleOAuthVersion}"
  compile "org.apache.logging.log4j:log4j-to-slf4j:2.8.2"
  compile "org.slf4j:slf4j-api:1.7.24"
  compile "org.slf4j:slf4j-simple:1.7.21"

  testCompile "org.elasticsearch.test:framework:${esVersion}"
  testCompile "org.elasticsearch.client:x-pack-transport:${xpackVersion}"
  testCompile "org.apache.logging.log4j:log4j-api:${versions.log4j}"
  testCompile "org.apache.logging.log4j:log4j-core:${versions.log4j}"

  testRuntime "org.elasticsearch.plugin:x-pack:${xpackVersion}@zip"
}

Map generateSubstitutions() {
  def stringSnap = { version ->
    if (version.endsWith("-SNAPSHOT")) {
      return version.substring(0, version.length() - 9)
    }
    return version
  }
  return [
    'version': stringSnap(version),
    'xpack.version': stringSnap(VersionProperties.elasticsearch),
    'java.version': targetCompatibility as String
  ]
}

processResources {
  MavenFilteringHack.filter(it, generateSubstitutions())
}

task buildZip(type:Zip, dependsOn: [jar]) {
  from 'build/resources/main/x-pack-extension-descriptor.properties'
  from 'build/resources/main/x-pack-extension-security.policy'
  from project.jar
  from configurations.runtime - configurations.provided
}

